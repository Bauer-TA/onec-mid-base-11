#Область СлужебныеПроцедурыИФункции
Процедура ДополнитьФорму (Форма) Экспорт 
	//++ Бауэр Татьяна, Дополняем форму полями и кнопкой, 19.01.2026
	ИмяФормы = Форма.ИмяФормы;

	Если ИмяФормы = "Документ.ОплатаОтПокупателя.Форма.ФормаДокумента" Тогда		
		ДобавитьКонтактноеЛицоПередСуммойДокумента(Форма);
	КонецЕсли; 
	
	Если ИмяФормы = "Документ.ЗаказПокупателя.Форма.ФормаДокумента" Тогда
		
		Группа = Форма.Элементы.Добавить("ГруппаШапкаСкидка", Тип("ГруппаФормы"), Форма.Элементы.ГруппаШапкаЛево);
		Группа.Вид = ВидГруппыФормы.ОбычнаяГруппа;
		Группа.ОтображатьЗаголовок = Ложь;
		
		ПолеВвода = Форма.Элементы.Добавить("СогласованнаяСкидка_ТБ", Тип("ПолеФормы"), Форма.Элементы.ГруппаШапкаСкидка);
		ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
		ПолеВвода.ПутьКДанным = "Объект.СогласованнаяСкидка_ТБ";
		ДобавитьОбработчикДействия(Форма);
		
		Команда = Форма.Команды.Добавить("ОбновитьЦены"); 
		Команда.Заголовок = "Обновить цены";
		Команда.Действие = "КомандаОбновитьЦены";
		
		Кнопка = Форма.Элементы.Добавить("КнопкаОбновитьЦены", Тип("КнопкаФормы"), Форма.Элементы.ГруппаШапкаСкидка);
		Кнопка.ИмяКоманды = "ОбновитьЦены";
		Кнопка.Вид = ВидКнопкиФормы.ОбычнаяКнопка;
		
		ДобавитьКонтактноеЛицоВГруппаШапкаПраво(Форма);
	КонецЕсли;
	
	Если ИмяФормы = "Документ.ОплатаПоставщику.Форма.ФормаДокумента" Тогда
		ДобавитьКонтактноеЛицоПередСуммойДокумента(Форма);
	КонецЕсли;
	
	Если ИмяФормы = "Документ.ПоступлениеТоваровУслуг.Форма.ФормаДокумента" Тогда
		ДобавитьКонтактноеЛицоВГруппаШапкаПраво(Форма);
	КонецЕсли;
	
	Если ИмяФормы = "Документ.РеализацияТоваровУслуг.Форма.ФормаДокумента" Тогда
		ДобавитьКонтактноеЛицоВГруппаШапкаПраво(Форма);
	КонецЕсли;
	//-- Бауэр Татьяна, 19.01.2026
КонецПроцедуры 

Процедура ДобавитьКонтактноеЛицоВГруппаШапкаПраво(Форма)
	//++ Бауэр Татьяна, Дополняем форму полями, 19.01.2026
	ПолеВвода = Форма.Элементы.Добавить("КонтактноеЛицо_ТБ", Тип("ПолеФормы"), Форма.Элементы.ГруппаШапкаПраво);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.КонтактноеЛицо_ТБ";
	//-- Бауэр Татьяна, 19.01.2026
	
КонецПроцедуры 

Процедура ДобавитьКонтактноеЛицоПередСуммойДокумента(Форма)

	//++ Бауэр Татьяна, Дополняем форму полями, 19.01.2026
	ПолеВвода = Форма.Элементы.Вставить("КонтактноеЛицо_ТБ", Тип("ПолеФормы"), , Форма.Элементы.СуммаДокумента);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.КонтактноеЛицо_ТБ";
	//-- Бауэр Татьяна, 19.01.2026

КонецПроцедуры 

Процедура ДобавитьОбработчикДействия(Форма) 
	//++ Бауэр Татьяна, Добовляем обработчик ПриИзменении к полю СогласованнаяСкидка_ТБ, 19.01.2026
	Если Форма.Элементы.Найти("СогласованнаяСкидка_ТБ") = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Форма.Элементы.СогласованнаяСкидка_ТБ.УстановитьДействие("ПриИзменении", "СогласованнаяСкидка_ТБПриИзменении");
	//-- Бауэр Татьяна, 19.01.2026
КонецПроцедуры

#КонецОбласти

